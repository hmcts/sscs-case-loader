plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.7.RELEASE'
  id 'org.springframework.boot' version '2.1.5.RELEASE'
  id 'org.owasp.dependencycheck' version '4.0.2'
  id 'com.github.ben-manes.versions' version '0.21.0'
  id 'net.ltgt.apt' version '0.21'
  id 'org.sonarqube' version '2.6.2'
  id "com.gorylenko.gradle-git-properties" version "2.0.0"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

sourceCompatibility = 1.8
compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation'
}

targetCompatibility = 1.8

mainClassName = 'uk.gov.hmcts.reform.sscs.CaseLoaderApp'

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/IntegrationTests/java')
    }
  }
  e2e {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/e2e/java')
    }
    resources {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/e2e/resources')
    }
  }
}

task integration(type: Test) {
  setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
  setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task smoke(type: Test) {
  group = 'verification'
  description = 'Executes non-destructive smoke tests against a running case loader'
  setTestClassesDirs(sourceSets.e2e.output.classesDirs)
  setClasspath(sourceSets.e2e.runtimeClasspath)
  include "uk/gov/hmcts/reform/sscs/smoke/**"
  exclude "uk/gov/hmcts/reform/sscs/functional/**"
}

task functional(type: Test) {
  group = 'Functional Tests'
  description = 'Executes non-destructive functional tests in AAT against a running case loader'
  setTestClassesDirs(sourceSets.e2e.output.classesDirs)
  setClasspath(sourceSets.e2e.runtimeClasspath)
  include "uk/gov/hmcts/reform/sscs/functional/**"
  exclude "uk/gov/hmcts/reform/sscs/smoke/**"
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.8'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.5.0"
  ignoreFailures = true
  sourceSets = [
    sourceSets.main,
    sourceSets.test,
    sourceSets.integrationTest,
    sourceSets.e2e
  ]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

dependencies {
  integrationTestCompile(sourceSets.test.output)
  e2eCompile(sourceSets.test.output)
}

jacoco {
  toolVersion = "0.8.1"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  maven {
    url "https://dl.bintray.com/hmcts/hmcts-maven"
  }

  maven {
    url 'https://repo.spring.io/libs-milestone'
  }

  jcenter()
  mavenCentral()
  mavenLocal()

  // jitpack should be last resort
  // see: https://github.com/jitpack/jitpack.io/issues/1939
  maven { url 'https://jitpack.io' }
}


project.tasks['sonarqube'].dependsOn test, integration
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

sonarqube {
  properties {
    property "sonar.projectName", "SSCS - Case loader"
    property "sonar.projectKey", "SSCSCL"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"
    property "sonar.exclusions", "**/sscs/models/**, **/sscs/exceptions/**, **/sscs/CaseLoaderApp.java"

  }
}


configurations {
  integrationTestCompile.extendsFrom(testCompile)
  integrationTestRuntime.extendsFrom(testRuntime)
  e2eRuntime.extendsFrom(testRuntime)
  e2eCompile.extendsFrom(testCompile)
}

bootRun {
  systemProperty 'server.port', '8082'
}

def springBootVersion = '2.1.5.RELEASE'

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
  }
}

def versions = [
  logback         : '1.2.3',
  reformLogging   : '5.0.1',
  springBoot      : springBootVersion,
  springHystrix   : '1.4.2.RELEASE',
  springfoxSwagger: '2.8.0'
]

ext {
  springCloudVersion = 'Greenwich.SR1'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
  dependencies {
    // checkstyle needs a certain version and so does spring fox swagger, so can't use latest
    // fun times...
    dependencySet(group: 'com.google.guava', version: '27.1-jre') {
      entry 'guava'
    }
    // CVE-2019-0232, CVE-2019-0199 - command line injections on windows
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.19') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-el'
      entry 'tomcat-embed-websocket'
    }
  }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  compile group: 'org.springframework', name: 'spring-context-support'

  compile group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
  compile group: 'ch.qos.logback', name: 'logback-core', version: versions.logback

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: versions.springHystrix
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard', version: versions.springHystrix

  compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.4.RELEASE'

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: '1.1.0'
  compile group: 'uk.gov.hmcts.reform', name: 'sscs-common', version: '3.0.3'

  compile group: 'com.jcraft', name: 'jsch', version: '0.1.55'
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.62'

  compile group: 'org.json', name: 'json', version: '20180813'

  compile group: 'commons-io', name: 'commons-io', version: '2.6'

  compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names'
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
  compile group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'

  compileOnly 'org.projectlombok:lombok:1.18.8'
  annotationProcessor "org.projectlombok:lombok:1.18.6"

  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: '2.6.1'
  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '2.5.1'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  testCompile 'pl.pragmatists:JUnitParams:1.1.1'

  runtime "org.springframework.boot:spring-boot-properties-migrator"
}

bootJar {
  archiveName 'sscs-case-loader.jar'
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': project.version
  }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

run {
  def debug = System.getProperty("debug")
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
  }
}
