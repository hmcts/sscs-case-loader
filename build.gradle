plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.3.5.RELEASE'
  id 'uk.gov.hmcts.java' version '0.12.12'
  id 'com.github.ben-manes.versions' version '0.41.0'
  id 'org.sonarqube' version '3.3'
  id 'org.owasp.dependencycheck' version '8.0.2'
  id "com.gorylenko.gradle-git-properties" version "2.3.2"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation'
}

mainClassName = 'uk.gov.hmcts.reform.sscs.CaseLoaderApp'

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/IntegrationTests/java')
    }
  }
  e2e {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/e2e/java')
    }
    resources {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/e2e/resources')
    }
  }
}

task integration(type: Test) {
  testLogging.showStandardStreams(true)
  testLogging.showStackTraces(true)
  outputs.upToDateWhen {false}
  setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
  setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task functionalPreDeploy(type: Test) {
  group = 'Functional Tests'
  description = 'Place delta files on the sftp server ready to be consumed when AKS job is created'
  setTestClassesDirs(sourceSets.e2e.output.classesDirs)
  setClasspath(sourceSets.e2e.runtimeClasspath)
  include "uk/gov/hmcts/reform/sscs/functional/predeploy/**"
  exclude "uk/gov/hmcts/reform/sscs/functional/postdeploy/**"
}

task functionalPostDeploy(type: Test) {
  group = 'Functional Tests'
  description = 'Verifies that files from functionPreDeploy step were processed following job creation in AKS'
  setTestClassesDirs(sourceSets.e2e.output.classesDirs)
  setClasspath(sourceSets.e2e.runtimeClasspath)
  include "uk/gov/hmcts/reform/sscs/functional/postdeploy/**"
  exclude "uk/gov/hmcts/reform/sscs/functional/predeploy/**"
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

pmd {
  toolVersion = "6.5.0"
  ignoreFailures = true
  sourceSets = [
    sourceSets.main,
    sourceSets.test,
    sourceSets.integrationTest,
    sourceSets.e2e
  ]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

dependencies {
  integrationTestImplementation (sourceSets.test.output)
  e2eImplementation (sourceSets.test.output)
}

jacoco {
  toolVersion = "0.8.8"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all {
      def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
      }
      if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 7-10 fails the build, anything lower and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 7.0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
  cveValidForHours = 4 // on Jenkins we have 24, so if you get more vulnerabilities locally, try to normalize this value with Jenkins

  analyzers {
    retirejs {
      enabled = false
    }
    ossIndex {
      enabled = false
    }
  }
}

repositories {
  mavenLocal()

  mavenCentral()

  maven {
    url 'https://repo.spring.io/libs-milestone'
  }

  // jitpack should be last resort
  // see: https://github.com/jitpack/jitpack.io/issues/1939
  maven { url 'https://jitpack.io' }
}

tasks.withType(Copy).all { duplicatesStrategy 'warn' }

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

sonarqube {
  properties {
    property "sonar.projectName", "SSCS - Case loader"
    property "sonar.projectKey", "SSCSCL"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.exclusions", "**/sscs/models/**, **/sscs/exceptions/**, **/sscs/CaseLoaderApp.java," +
      " **/sscs/services/xml/XmlValidator.java"
  }
}


configurations {
  integrationTestImplementation .extendsFrom(testImplementation )
  integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
  e2eRuntimeOnly.extendsFrom(testRuntimeOnly)
  e2eImplementation .extendsFrom(testImplementation )
}

ext["rest-assured.version"] = '4.3.0'

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR12"
    mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBoot.class.package.implementationVersion}"
  }
  dependencies {
    dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    dependency group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
    dependency group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.17.1'

    // checkstyle needs a certain version and so does spring fox swagger, so can't use latest
    // fun times...
    dependencySet(group: 'com.google.guava', version: '31.0.1-jre') {
      entry 'guava'
    }
    // until 9.0.29 - CVE-2019-12418, CVE-2019-17563
    dependencySet(group: 'org.apache.tomcat.embed', version: '10.1.1') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-websocket'
    }
    //CVE-2021-22112
    dependencySet(group: 'org.springframework.security', version: '5.7.5') {
      entry 'spring-security-crypto'
    }

    //CVE-2021-22118
    dependencySet(group: 'org.springframework', version: '5.2.22.RELEASE') {
      entry 'spring-aop'
      entry 'spring-aspects'
      entry 'spring-beans'
      entry 'spring-context'
      entry 'spring-context-support'
      entry 'spring-core'
      entry 'spring-expression'
      entry 'spring-jcl'
      entry 'spring-jdbc'
      entry 'spring-jms'
      entry 'spring-messaging'
      entry 'spring-orm'
      entry 'spring-test'
      entry 'spring-tx'
      entry 'spring-web'
      entry 'spring-webmvc'
    }

    //CVE-2021-27568
    dependencySet(group: 'net.minidev', version: '2.4.7') {
      entry 'json-smart'
      entry 'accessors-smart'
    }

    //CVE-2021-27807
    dependencySet(group: 'org.apache.pdfbox', version: '2.0.25') {
      entry 'pdfbox'
    }

     dependencySet(group: 'org.apache.xmlgraphics', version: '1.14') {
      entry 'batik-all'
    }

    dependencySet(group: 'org.apache.commons', version: '1.21') {
      entry 'commons-compress'
    }

    //CVE-2022-25857, CVE-2022-38749, CVE-2022-38750, CVE-2022-38751
    dependencySet(group: 'org.yaml', version: '1.33') {
      entry 'snakeyaml'
    }

    //CVE-2021-28170
    dependency group: 'org.glassfish', name: 'jakarta.el', version: '4.0.1'

    //CVE-2021-40690
    dependency group: 'org.apache.santuario', name: 'xmlsec', version: '2.3.0'

    //CVE-2021-42550
    dependencySet(group: 'ch.qos.logback', version: '1.2.11') {
        entry 'logback-core'
        entry 'logback-classic'
    }

    //CVE-2022-40149, CVE-2022-40150, CVE-2022-45685, CVE-2022-45693
    dependency group: 'org.codehaus.jettison', name: 'jettison', version: '1.5.3'

  }
}

dependencies {
  implementation  group: 'org.springframework.boot', name: 'spring-boot-starter-batch'
  implementation  group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  implementation  group: 'org.springframework.batch', name: 'spring-batch-test'
  implementation group: 'com.h2database', name: 'h2'

  implementation  group: 'org.unix4j', name: 'unix4j-command', version: '0.6'

  implementation  group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation  group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation  group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation  group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  implementation  group: 'org.springframework', name: 'spring-context-support'

  implementation  group: 'com.github.hmcts', name: 'properties-volume-spring-boot-starter', version: '0.1.0'

  implementation  group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: '1.4.2.RELEASE'
  implementation  group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard', version: '1.4.2.RELEASE'

  implementation  group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.1'

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '2.1.1'
  implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.8.6'

  implementation  group: 'com.github.hmcts', name: 'java-logging', version: '5.1.7'
  implementation  group: 'com.github.hmcts', name: 'sscs-common', version: '9c79807'

  implementation  group: 'com.jcraft', name: 'jsch', version: '0.1.55'
  implementation  group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'

  implementation  group: 'org.json', name: 'json', version: '20211205'

  implementation  group: 'commons-io', name: 'commons-io', version: '2.11.0'
  implementation  group: 'org.elasticsearch', name: 'elasticsearch', version: '7.16.3'

  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
  implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'


  // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.1'
  // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-annotations
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.14.1'
  // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.1'
  // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-cbor
  implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version: '2.14.1'

  implementation  group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
  testImplementation  group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
  e2eImplementation  group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
  e2eAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

  testImplementation  group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: '2.28.0'
  testImplementation  group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '2.28.0'
  testImplementation  group: 'io.rest-assured', name: 'rest-assured'
  testImplementation  group: 'com.github.hmcts', name: 'fortify-client', version: '1.2.2', classifier: 'all', {
    exclude group: 'commons-io', module: 'commons-io'
  }
  implementation  group: 'commons-io', name: 'commons-io', version: '2.11.0'
  integrationTestImplementation  group: 'commons-io', name: 'commons-io', version: '2.11.0'

  testImplementation  group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  testImplementation  group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

  runtimeOnly group: 'org.springframework.boot', name: 'spring-boot-properties-migrator'
}

bootJar {
  archiveName 'sscs-case-loader.jar'
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': project.version
  }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

run {
  def debug = System.getProperty("debug")
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
  }
}
