plugins {
  id 'application'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.3.5.RELEASE'
  id 'uk.gov.hmcts.java' version '0.12.12'
  id 'com.github.ben-manes.versions' version '0.41.0'
  id 'net.ltgt.apt' version '0.21'
  id 'org.sonarqube' version '3.3'
  id 'org.owasp.dependencycheck' version '6.2.2'
  id "com.gorylenko.gradle-git-properties" version "2.3.2"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.hmcts.reform'
version = '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation'
}

mainClassName = 'uk.gov.hmcts.reform.sscs.CaseLoaderApp'

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/IntegrationTests/java')
    }
  }
  e2e {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/e2e/java')
    }
    resources {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir('src/e2e/resources')
    }
  }
}

task integration(type: Test) {
  setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
  setClasspath(sourceSets.integrationTest.runtimeClasspath)
}

task functionalPreDeploy(type: Test) {
  group = 'Functional Tests'
  description = 'Place delta files on the sftp server ready to be consumed when AKS job is created'
  setTestClassesDirs(sourceSets.e2e.output.classesDirs)
  setClasspath(sourceSets.e2e.runtimeClasspath)
  include "uk/gov/hmcts/reform/sscs/functional/predeploy/**"
  exclude "uk/gov/hmcts/reform/sscs/functional/postdeploy/**"
}

task functionalPostDeploy(type: Test) {
  group = 'Functional Tests'
  description = 'Verifies that files from functionPreDeploy step were processed following job creation in AKS'
  setTestClassesDirs(sourceSets.e2e.output.classesDirs)
  setClasspath(sourceSets.e2e.runtimeClasspath)
  include "uk/gov/hmcts/reform/sscs/functional/postdeploy/**"
  exclude "uk/gov/hmcts/reform/sscs/functional/predeploy/**"
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

pmd {
  toolVersion = "6.5.0"
  ignoreFailures = true
  sourceSets = [
    sourceSets.main,
    sourceSets.test,
    sourceSets.integrationTest,
    sourceSets.e2e
  ]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

dependencies {
  integrationTestCompile(sourceSets.test.output)
  e2eCompile(sourceSets.test.output)
}

jacoco {
  toolVersion = "0.8.2"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all {
      def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
      }
      if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'dependency-check-suppressions.xml'
}

repositories {
  mavenLocal()

  mavenCentral()
  jcenter()

  maven {
    url 'https://repo.spring.io/libs-milestone'
  }

  // jitpack should be last resort
  // see: https://github.com/jitpack/jitpack.io/issues/1939
  maven { url 'https://jitpack.io' }
}


project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)

sonarqube {
  properties {
    property "sonar.projectName", "SSCS - Case loader"
    property "sonar.projectKey", "SSCSCL"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.exclusions", "**/sscs/models/**, **/sscs/exceptions/**, **/sscs/CaseLoaderApp.java," +
      " **/sscs/services/xml/XmlValidator.java"
  }
}


configurations {
  integrationTestCompile.extendsFrom(testCompile)
  integrationTestRuntime.extendsFrom(testRuntime)
  e2eRuntime.extendsFrom(testRuntime)
  e2eCompile.extendsFrom(testCompile)
}

def versions = [
  logback         : '1.2.3',
  reformLogging   : '5.1.7',
  springBoot      : springBoot.class.package.implementationVersion,
  springHystrix   : '1.4.2.RELEASE',
  springfoxSwagger: '2.8.0',
  sscsCommon      : '4.8.6',
]

ext["rest-assured.version"] = '4.3.0'

ext {
  springCloudVersion = 'Hoxton.SR12'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    mavenBom "org.springframework.boot:spring-boot-starter-parent:${versions.springBoot}"
  }
  dependencies {
    dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    dependency group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
    dependency group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.17.1'

    // checkstyle needs a certain version and so does spring fox swagger, so can't use latest
    // fun times...
    dependencySet(group: 'com.google.guava', version: '31.0.1-jre') {
      entry 'guava'
    }
    // until 9.0.29 - CVE-2019-12418, CVE-2019-17563
    dependencySet(group: 'org.apache.tomcat.embed', version: '10.0.12') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-websocket'
    }
    //CVE-2021-22112
    dependencySet(group: 'org.springframework.security', version: '5.6.1') {
      entry 'spring-security-crypto'
    }

    //CVE-2021-22118
    dependencySet(group: 'org.springframework', version: '5.2.21.RELEASE') {
      entry 'spring-aop'
      entry 'spring-aspects'
      entry 'spring-beans'
      entry 'spring-context'
      entry 'spring-context-support'
      entry 'spring-core'
      entry 'spring-expression'
      entry 'spring-jcl'
      entry 'spring-jdbc'
      entry 'spring-jms'
      entry 'spring-messaging'
      entry 'spring-orm'
      entry 'spring-test'
      entry 'spring-tx'
      entry 'spring-web'
      entry 'spring-webmvc'
    }

    //CVE-2021-27568
    dependencySet(group: 'net.minidev', version: '2.4.7') {
      entry 'json-smart'
      entry 'accessors-smart'
    }

    //CVE-2021-27807
    dependencySet(group: 'org.apache.pdfbox', version: '2.0.25') {
      entry 'pdfbox'
    }

     dependencySet(group: 'org.apache.xmlgraphics', version: '1.14') {
      entry 'batik-all'
    }

    dependencySet(group: 'org.apache.commons', version: '1.21') {
      entry 'commons-compress'
    }

    //CVE-2021-28170
    dependency group: 'org.glassfish', name: 'jakarta.el', version: '4.0.2'

    //CVE-2021-40690
    dependency group: 'org.apache.santuario', name: 'xmlsec', version: '2.3.0'

  }
}

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
        details.useVersion '2.11.0'
      }
      if (details.requested.group == 'org.codehaus.groovy') {
        details.useVersion "3.0.7"
        details.because "needed by rest-assured>=4.3"
      }
    }
  }
}

dependencies {

  compile 'org.springframework.boot:spring-boot-starter-batch'
  compile 'org.springframework.boot:spring-boot-starter-test'
  compile 'org.springframework.batch:spring-batch-test'
  compile 'com.h2database:h2'


  compile 'org.unix4j:unix4j-command:0.6'

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  compile group: 'org.springframework', name: 'spring-context-support'

  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.1.0'

  compile group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback
  compile group: 'ch.qos.logback', name: 'logback-core', version: versions.logback

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: versions.springHystrix
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard', version: versions.springHystrix

  compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.1'
  compile group: 'com.github.hmcts', name: 'java-logging', version: versions.reformLogging
  compile group: 'com.github.hmcts', name: 'sscs-common', version: versions.sscsCommon

  compile group: 'com.jcraft', name: 'jsch', version: '0.1.55'
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'

  compile group: 'org.json', name: 'json', version: '20211205'

  compile group: 'commons-io', name: 'commons-io', version: '2.11.0'
  compile group: 'org.elasticsearch', name: 'elasticsearch', version: '8.1.3'

  compileOnly 'org.projectlombok:lombok:1.18.22'
  annotationProcessor "org.projectlombok:lombok:1.18.22"
  testCompileOnly 'org.projectlombok:lombok:1.18.18'
  testAnnotationProcessor "org.projectlombok:lombok:1.18.18"
  e2eCompile 'org.projectlombok:lombok:1.18.18'
  e2eAnnotationProcessor "org.projectlombok:lombok:1.18.18"

  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: '2.28.0'
  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '2.28.0'
  testCompile group: 'io.rest-assured', name: 'rest-assured'
  testCompile group: 'com.github.hmcts', name: 'fortify-client', version: '1.2.2', classifier: 'all', {
    exclude group: 'commons-io', module: 'commons-io'
  }
  compile group: 'commons-io', name: 'commons-io', version: '2.11.0'
  integrationTestCompile group: 'commons-io', name: 'commons-io', version: '2.11.0'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  testCompile 'pl.pragmatists:JUnitParams:1.1.1'

  runtime "org.springframework.boot:spring-boot-properties-migrator"

}

bootJar {
  archiveName 'sscs-case-loader.jar'
  manifest {
    attributes 'Implementation-Title': project.name,
      'Implementation-Version': project.version
  }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

run {
  def debug = System.getProperty("debug")
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
  }
}
